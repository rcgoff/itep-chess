;Программа А - вариант для 9 ячеек (4-й)
 ;Программа А предназначена для подготовки к десятичной печати
 ;двух младших адресов командной ячейки, предварительно пересланной
 ;в ячейку αпеч.
 ;В программе А адреса разбиваются на триады, перед каждой триадой ставится
 ;одноразрядный нуль; кроме того, между тетрадами, соответствующими
 ;среднему и правому адресам, вставляется дополнительная тетрада, дающая
 ;интервал при десятичной печати; такая же тетрада ставится в коде.
 ;Результат помещается в ячейку γпеч, выход из программы осуществляется
 ;через стандартную ячейку Ω.
 ;Записано в виде, пригодном к ассемблированию автокодом М-20
 ;авторства Д.Стефанкова.
 ;Л.Ядренников 20.12.2021-10.01.2022
 ;
 ;
 ;Пояснение к команде сдвига СДСА (коп 54):
 ;старший разряд определяет знак.
 ;s=Iа-100, s>0 (т.е. ст.разр.=1) - сдвиг влево (в сторону старших разрядов).
 ;ω=1, если в сдвигаемой части получился 0.
 ;
	.ВСТАВИТЬ "../../b61_const.inc"
	.ПЕЧАТЬ
	.АДРЕС 200
	.СТАРТ 200
А_:	или	0,	Ω,	конец_А
	сдса 	103,	{F.F.F},R2		;R2=007 7777 7777 7770
	и	αпеч,	{0.F.0},γпеч		;высекли IIа исходной информации
	сдса	103,	γпеч,	γпеч		;γпеч=000 000исх4 исх3исх2исх10 0000, подготовили пропуск между адресами
	и	αпеч,	{0.0.F},R1		;IIIа исходной в R1
рабцикл: см	R1,	γпеч,	γпеч		;образуем очередную тетраду: триады сдвигаются на (дв.)разряд влево, тетрады остаются на месте
						;сдвиг триад достигается за счет сложения с самими собой
						;ноль (добавляемый к триаде) получается сам собой как результат сдвига при помощи сложения
	и	R2,	γпеч,	R1		;высекаем из γпеч еще не обработанные триады для след прохода
	сдса	104,	R2,	R2		;сдвигаем высекатель на тетраду
	пу	рабцикл,рабцикл,Ω-1		;остались еще тетрады?
						;команда "рабцикл" в Ω-1 обеспечит добавление пропуска (0340)
	пв	340,	Ω-1,	R1		;0340 - это 1110 (0eh) в том месте, где должен стоять пропуск
конец_А: .ПАМ 1 	
	.ФИНИШ 200
