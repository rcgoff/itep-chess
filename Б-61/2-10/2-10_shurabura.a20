; Стандартная программа 10: перевод 2 -> 10

;Автор программы - Шура-Бура М.Р., 1958
;Первоначально код записан и протестирован в 2015г. Стефанковым Д.В.
;взято из файла is2_build.m20, тест программы см. is2_test_0011.
;Дизассемблировано, прокомментировано, переработано
;применительно к стандартам и константам Б-61
;12-28.01.2022 - Ядренников Л.О.
;21.05.2022 - изменены раб. ячейки (R5 вместо R1, R10 вместо R2)
;	для работы с Пq1

;----------------------------------------------описание из книги
;---------------------- "Библиотека стандартных программ", 1961, 
;---------------------- стр. 44-46.
;Перевод осуществляется следующим образом.
;Прежде всего исходное число множится на 1/10 для того, чтобы
;при последующем умножении на 10 не получить машинного переполнения
;в случае больших чисел (x(2)*10>2^64).
;Затем полученное число множится на 10 или 1/10 в зависимости
;от того, больше оно 1 или нет до тех пор, пока результат
;умножений не попадет в интервал [0.1;1].
;Количество произведенных умножений определяет |p(10)|.
;|p(10)| положительно при исходном числе больше 1 и отрицательно
;в противном случае.
;Полученное в результате умножений число и будет десятичной мантиссой
;q(10).
;Цифры десятичной мантиссы α1, α2...α9 определяются последовательно
;путем умножения q(10) на 10 и выделения целой части.
;Схема программы
;1 - подготовительные операции (в частности, умножение исходного числа на 1/10)
;2 - цикл умножения на 10
;3 - цикл умножения на 1/10
;4 - получение p(10) в двоично-десятичном виде
;5 - округление q(10)
;6 - цикл выделения десятичных цифр мантиссы
;Рабочие ячейки R5,R10.

	.АДРЕС 0361
	.СТАРТ 0361
2>10_:	или	0 Ω конец_2>10
	ра	(0) 0 предконец_2>10
 
	и	αпеч {7.00.F.F.F} R5	;очистка p(10)
	у	αпеч 0.1d R10	;x 1/10
 
	у	R10 10d R10	;цикл умножений на 10
	пум	24 *-1 (1) ;LY: стоп цикла при РА >=24oct=20dec (max порядок = +-19)
 
	пм	2 * (2)  ;РА'=РА+2; при |x|>0.1 РА'=5
 
 ;команды циклов по РА обновляют РА 3-м адресом в любом случае, независимо от того, выполняется условие перехода или нет
 ;поэтому число в РА равно количеству умножений.
 ;условие для зацикливания выполнится, если исходное число |x|<1, при этом после x0.1 |x|<0.1
 ;и после 1-го x10 число все еще меньше 1. 
 ;т.е. при |x|<1 РА по выходу из цикла >=2, а в остальных случаях, при бОльшем числе, РА=1.
 ;после ПМ  РА для |x|<1  = РА+2, а если |x|>1, то выполняется условие ПМ (РА=1, что меньше 2)
 ;и за два прохода ПМ становится РА=5, о чем и писал Шура-Бура в комменте, только у него число уже после x0.1 
 
	у	R10 0.1d R10	   ;цикл умножений на 1/10
	пен	(0) *-1 (1)  ;LY: РА-часть условия ПЕН РА>=А1 выполняется всегда, так что цикл идет только, пока число >1
 
 ;после обоих циклов получили в РА модуль дес.порядка +5, 
 ;т.е. для 0.1<|x|<1 РА=5, а для |х| вне этого интервала - в обе стороны увеличивается
 
	спа	(73) R5 R5	;получение p(10) порядка
 ;вычли из порядка 5 (сложение с 73 даст 100+порядок, но в СПА используются только 6 разрядов, т.ч. 0 вместо 100)
 
	пн	17 *-1 13	;получение bcd порядка
 ;BCD преобразование для порядков >9 (а РА>14dec=16oct, т.е. не меньше 0017 - A1)
 ;для них, чтобы получилось BCD, нужно к порядку в дв.форме, кот. уже в R5, прибавить 6,
 ;что реализовано как засылка в РА 13oct=11dec и вычитание 5 предыдущей командой СПА. Лихо!
 
 ;-----------------------------------------
 ;теперь в R5 правильный модуль дес.порядка в BCD, остальные разряды - исходные
 ;в R10 дес.мантисса в дв.виде в интервале 0.1...1
 
	н	R5 {1.00.0.0.0} γпеч	;знак порядка ;LY: знаки дв. и дес. порядка совпадают,
 					;но из-за особенностей представления дв.чисел в М-20 
 					;знак дв.порядка должен быть проинвертирован
	или	R10 for_round R10	;округление ;LY и отрицательный знак!
	сн	R10 {1.00.0.0.0} R10	;денормализация десятичной мантиссы
 ;для интервала 0.1..1 мантисса попадает в четыре интервала дв. порядков
 ;0.5..1; 0.25..0.5; 0.125...0.25; 0.0625...0.125.
 ;нам нужно, чтобы мантисса была для дес. порядка -1, для чего она
 ;денормализуется для дв. порядка -1. Это достигается сложением с указанным
 ;числом, где порядок как раз такой, а мантиссы нет
 ;произойдет выравнивание порядка на бОльший (на 2^-1),
 ;самое большее три сдвига. А нормализация результата отключена. 
 
 ;-------------------------
 ;теперь в R10 дв.мантисса, соотв.дес.порядку, 
 ;а в γпеч правильный знак порядка, BCD дес.порядок, остальное исходное
 ;после BCD-коррекции порядка в РА остается 0013
 
	;цикл выделения десятичных цифр (9 раз)
BCDloop: уон	R10 int10 R5	
	мрп	0 0 R10
 ;теперь в R5 в мл.разрадах очередная цифра (4 разряда), 
 ;а в R10 остальное (хвост), готовое к новым умножениям
 
	сдмп	10d γпеч γпеч	
 ;использовали то, что 10 имеет дв.порядок 104, на который и надо сдвинуть влево мантиссу
 ;для подготовки места под очередную цифру (старший 1 задает сдвиг влево,04 - 4 разр)
 ;цифры исходной мантиссы в γпеч в каждом проходе постепенно вытесняются нулями и в итоге уйдут
 
	см	γпеч R5 γпеч	        ;снесли очередную цифру в младшие разряды γпеч
	пм	23 BCDloop (1)	;после 8 прохода цикла РА=23 (13oct+10oct) и в 9 проходе повторения не будет
 
предконец_2>10:	.ПАМ 1
конец_2>10:	.ПАМ 1
 
 ;константы
int10:     1 00	0000 0000 0012	;10 ненормализованное для умножения при выделениии BCD цифр
for_round: 2 00	0000 0000 0077
{1.00.0.0.0}: 1 00 0000 0000 0000
{7.00.F.F.F}: 7 00 7777 7777 7777

	.ФИНИШ 0361


;	.НЕПЕЧАТЬ
;стандартные ячейки Б-61
	.АДРЕС 0007
Ω:	.ПАМ 1
	.АДРЕС 0010
R0:	.ПАМ 1
R1:	.ПАМ 1
R2:	.ПАМ 1
R3:	.ПАМ 1
R4:	.ПАМ 1
R5:	.ПАМ 1
R6:	.ПАМ 1
R7:	.ПАМ 1
R10:	.ПАМ 1
	.АДРЕС 0025
αпеч:	.ПАМ 1
γпеч:	.ПАМ 1


;константы Б-61
	.АДРЕС 0075
1/16d:  0 75 4000 0000 0000
1/8d:   =0.125
1/4d:   0 77 4000 0000 0000
1/2d:   1 00 4000 0000 0000

	.АДРЕС 0101
1d:	1 01 4000 0000 0000
2d:	1 02 4000 0000 0000
3d:	1 02 6000 0000 0000
4d:	1 03 4000 0000 0000
5d:	1 03 5000 0000 0000
6d:	1 03 6000 0000 0000
7d:	1 03 7000 0000 0000
8d:	1 04 4000 0000 0000
9d:	1 04 4400 0000 0000
10d:	1 04 5000 0000 0000
1/3d:	0 77 5252 5252 5253
0.1d:   =0.1
0.01d:  =0.01
0.001d:	0 67 4061 1156 4571

	.АДРЕС 0131
{0.0.F}: 0 00 0000 0000 7777
{0.F.0}: 0 00 0000 7777 0000
{0.F.F}: 0 00 0000 7777 7777
{F.0.0}: 0 00 7777 0000 0000
{F.0.F}: 0 00 7777 0000 7777
{F.F.0}: 0 00 7777 7777 0000
{F.F.F}: 0 00 7777 7777 7777

;заголовки Б-61
	.АДРЕС 7747
биб2>10: пб	0,	2>10_,	0
	.АДРЕС 7751
Пq1:	.ПАМ 1
Пq2:	.ПАМ 1
	.АДРЕС 7764
А:	пб	0,	А_,	0

;эрзац-заголовок 2->10
	.АДРЕС 7624
эрз2>10: пб	0,	2>10_,	0
