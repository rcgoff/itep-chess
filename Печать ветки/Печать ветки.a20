;Программа Печать ветки
;АрлаДисс стр.36:
;(Ветка2?) - LY
;Вслед за печатью хода выдает оптимальную цепочку ходов, приводящую к позиции, 
;оценка которой и есть оценка сделанного хода. 
;(Ветка1?) - LY
;Кроме того, "Печать ветки" позволяет, при желании, выпечатывать ходы до заданного уровня,
;а дальше указывает оптимальную цепочку либо закрывающий ход.
;LY:"либо" означает ориентацию на ДЗУ-2?

;LY:
;обращение к программе: имеются два заголовка "Ветка 1" и "Ветка 2".
;внешние программы обращаются только к ним,
;а они уже вызывают "Печать ветки".

;"Ветка 2" безусловно вызывается из Внешней схемы,
;как и сказано в АрлаДисс и повторено выше,
;сразу вслед за Печатью хода.
;При этом выполняется нижняя часть Печати ветки и выход.

;"Ветка 1" безусловно вызывается из Хода,
;ближе к его концу.
;выполняется Печать ветки с самого начала,
;условно (с ячейкой Уровень) вызывается Печать, отрабатывает, если стоит ДЗУ2, и из нее выходим.
;иначе (Уровень больше) проходит Печать ветки, после чего безусловно вызывается Печать, и из нее выходим.
;в печати ветки, в подменной программе П3, может произойти обычное преобразование 2->10, может - подменное

;если при каждом Шлепе печатается ход (по Ветке1), это получается мы печатаем все вообще сделанные при переборе ходы?!


;в ФА у нас конструкция R0
;                       (1)
;в Ветке2 R0=E0, т.е. E0/ (1).
;в зависимости от РА имеем
;РА=7777 вет0/0 (но так не будет, РА предварительно очищается)
;РА=0 вет0/0+1
;РА=1 вет0/0+2
;РА=2 вет0/0+3...

;в ветке1 R0=(E2), т.е. (E2)/(1)
;до цикла:
;РА=7777 вет1/0-1
;РА=0 вет2/0-1
;РА=1 вет3/0-1
;РА=2 вет4/0-1...
;далее в циклах пойдет увеличение на 1, т.е. ветi/0, ветi/0+1 и т.д.


;Смысл переменных
;n: IIa - уровень перебора
;цвет: IIа=1 - белые, 0 - черные, остальное нули
;Ei содержат в IIа адрес вет i/0-i,
;	что дает возможность по команде ФА с РА=i выбрать информацию
;	из ячейки вет i/0.
;вет i/0 содержит во IIа номер текущего хода на i-м уровне, начиная с 1,
;то есть адрес его в массиве ходов ai

;Ветка1 использует РА (в Ветке2 он очищается)

;Л.Ядренников 22.05.2022-

 ;ячейки тела других программ
	.АДРЕС 4370
E0:	Стоп	0	вет0/0	0
E1:	Стоп	0	вет1/0-1	0
E2:	Стоп	0	вет2/0-2	0
E3:	Стоп	0	вет3/0-3	0
E4:	Стоп	0	вет4/0-4	0
E5:	Стоп	0	вет5/0-5	0

	.АДРЕС 4066
вет0/0:	.ПАМ 1		;после нее до вет0/6 можно
	.АДРЕС 4413	
вет1/0:	.ПАМ 1		;после нее до вет1/13oct можно
	.АДРЕС 4364	
вет2/0:	.ПАМ 1		;до вет2/3
	.АДРЕС 4325	
вет3/0:	.ПАМ 1		;после нее до вет 3/3 можно	
	.АДРЕС 4427
вет4/0:	.ПАМ 1		;до вет4/1
	.АДРЕС 4431	
вет5/0:	.ПАМ 1		;только вет5/0
;в целом не похоже, что ветi/0 - это начала массивов! разные цифры и меняются не монотонно



	.АДРЕС 4623
	.СТАРТ 4623
ветка_1:
	и	Печать_ветки_21	{F.0.F}	Печать_ветки_21	;отключить салют
	пв	Печать_3	Печать_ветки	Сч99
ветка_2:
	или	0	αвыд	γ1
	пу	0	Ω	РА				;очистка РА и выход, если αвыд<>0
	или	Печать_ветки_21	{0.4000.0}	Печать_ветки_21	;включить салют
	или	0	E0	R0			;E0 - это ячейка, содержащая адрес вет0/0 в программе Ветка
	или	0	n	γ0
	пб	Ω	Печать_ветки_14	Сч99	;Сч99 играет роль конца



	.АДРЕС 4645
Печать_ветки:
	или	0	Ω	Конец_Печать 
	вм	n	цвет	РА
	вм	РА	Уровень	0				;установка ω
	пу	{0.1.1}	Печать	γ1                              ;вызов Печать, если (n-цвет)<Уровень (Арладисс:"выпечатывать ходы до заданного уровня")
	вм	λ	(λзап)	R0
	см	{144.0.0.0}	R0	R0
	с	R0	0	δ0
	вм	n	{0.1.0}	γ0
	или	0	λ	(λзап)
	пб	(E2)	*+2	R0			;первый проход ФА-цикла выполнится: если РА=0, то вет2/0-1; если РА=1, то вет3/0-2, если РА=7777, то вет1/0
	см	γ1	{0.1.1}	γ1
;точка входа из Ветки2
;при обращении из Ветки2 РА=0, γ0=n, γ1=0, R0=E0 и цикл (ФА) выполнится только 1 раз,
;если *(&вет0/0+1) =0
Печать_ветки_14:
	фа	0	R0	γ1
	или	0	(1)	δ0			;это заготовка для ФА
							;для Ветки2 сформируется команда или 0,вет0/0+1,δ0
							;что хранится в вет0/0+1?
	пум	4	*-3	(1)                     ;как только результат будет с ω=1 (нулевой), т.е. операнд нулевой - выйдем из цикла 
	пв	П3	*+1	биб2>10			;подмена 2->10 на П3
	пв	*+2	Пq2	Ω
Печать_ветки_21:
	Стоп	δ0	0	γ1                      ;печать массива: от δ0 и столько, сколько в IIIа γ1
							;для Ветки2 это значит столько, сколько в IIIа αвыд, а αвыд=0
	пб	n	Сч99	РА

	.ВСТАВИТЬ "П3.a20"
	.ПЕЧАТЬ

;-----------------------------------------
;в зависимости от знака (цвет-n) либо печатает ячейку P0
;(преобразованную c помощью П4),
;либо нули.
;очевидно, ячейка P0 имеет смысл хода (см. описание в П4):
;в коде - куда пошла фигура, в ср.адресе - номер фигуры или характер хода
;восстанавливает реальный адрес 2->10 в заголовках Б-61
;возможно отключение печати по ДЗУ

;ячейка P0 заполняется в программе Шлёп
	.АДРЕС 3362
Печать:	и	ДЗУ2	{0.1.0}	0
	пе	n	Ω	РА			;если 1р IIа ДЗУ2 =1, работаем, иначе выходим
Печать_3:
	пв	П4	*+1	биб2>10
	вм	цвет	n	R0
	пу	{0.F.0}	Печать_11	R1
	вм	R0	{F.F.0}	0
	пу	{0.1.0}	*+2	R1
	или	0	0	R1
Печать_11:
	см	{ρ0.0.0}	R1	Печать_{13}
	пв	*+2	Пq1	Ω
Печать_{13}:	.ПАМ1
	пв	2->10 (реальн.нач.Б2)	Конец_Печать 	биб2>10
Конец_Печать:	.ПАМ1
{ρ0.0.0}:
	Стоп	P0	0	0

	.ВСТАВИТЬ "П4.a20"
 ;в оригинальной программе после П4 размещалась константа {7760.0.0377} несмотря на то, что она есть в общем блоке констант
 ;надо бы ее оставить, а то нарушается идентичность кода распознанному (?)



  	.НЕПЕЧАТЬ
	.ВСТАВИТЬ "../b61_const.inc"
	.ВСТАВИТЬ "../chess_const.inc"
	.ФИНИШ 4623
