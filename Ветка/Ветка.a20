;Программа Ветка вызывается из Минимакса
;(обращение идет к веткаΔ)

;вроде как программа заполняет ветi/0 номером текущего рассматриваемого хода из всех выписанных

;программа требует отображения РК на 7777 и РА на 7776.

;цитата из 508 препринта: если запись в РК происходит
;на команде передачи управления, то сначала выполняется 
;засланная в РК команда, а затем происходит передача управления.
;Если засланная команда является также командой
;передачи управления,то управление предается согласно указанию
;засланной в РК команды.

;получается, что команда в строке 15 сначала выполнит вычитание мантисс
;(строка Минимакс_2, ну или Минимакс+1) и выработает ω,
;а затем уйдет на Минимакс_7, т.е. на выход

;Л.Ядренников 27.05.2022-


	.АДРЕС 4066
вет0/0:	.ПАМ 1
	.АДРЕС 4413
вет1/0:	.ПАМ 1
	.АДРЕС 4364
вет2/0:	.ПАМ 1
	.АДРЕС 4325
вет3/0:	.ПАМ 1
	.АДРЕС 4427
вет4/0:	.ПАМ 1
	.АДРЕС 4431
вет5/0:	.ПАМ 1


	.АДРЕС 4630
	.СТАРТ 4630
Ветка:		сдма	0130	(a0-1)	R2  			;сдвиг влево на 2 адреса, от ai-1 останется IIIа на месте I
		вм	РА	цвет	РА			;цвет - это 0 (ч) или 1 (б) в IIа (?зачем эта команда?)
		пн	0006	Ветка_14	(0)		;уйти, если i>5
		или	0	(E0)	R3
		или	0	(E1)	R1
		фа	n	0	R3
		вм	a0	R2	(0)
;a0, модифицированный IIa n, т.е. ai,
;вычитается из R2 - т.е. из ai-1 c замененным Iа - в нем будет IIIа.
;по документации в IIIа записан адрес начала массива ходов след уровня, 
;а в Ia - место очередного неизученного хода текущего уровня.
;мы вычитаем из адреса неизученного хода уровня i адрес начала ходов уровня i, 
;т.е. получаем номер текущего хода на i-м уровне, начиная с 1,
;и пишем его в в ветi/0.
;точнее про адрес: сначала в "Ветка_4:" в R3 оказывается содержимое Ei (i - текущий уровень)
;затем по ФА берется (0), превратившись в i, и прибавляется ко IIа Ei.
;в итоге получается, что номер текущего хода в ветке запишется в ветi/0.
		пн	0005	Ветка_14	(1)		;инкремент РА и когда заполнено до E5, перейти
		фа	R1	0	R3
		пб	(0)	*-2	(0)
;переход выполняется всегда (не модифицируется по ФА), 
;выполняется пересылка из ветi+2/0 в ветi+1/0, пока i<5 

;точка входа из Минимакса, РА=уровень перебора 
веткаΔ:		вм	0	(a0)	0			;ai <> 0? (?получается, рассматривались ли ходы на этом уровне?)
Ветка_14:	пе	n	Ветка	РА                      ;да - уйти на заголовок Ветки
								;если мы сюда попали из заголовка Ветки, то ω установится по Ветка_2
								;ω=1 если цвет>n, на 0-м уровне только
								;если мы сюда попали из 
		пб	Минимакс+1	Минимакс_7	РК      ;сформир. ω=(1 или 0) случаев 1 или 2А по условиям Минимакса и выйти через него
E0:		Стоп	0000	вет0/0	0000
E1:		Стоп	0000	вет1/0-1	0000
E2:		Стоп	0000	вет2/0-2	0000
E3:		Стоп	0000	вет3/0-3	0000
E4:		Стоп	0000	вет4/0-4	0000
E5:		Стоп	0000	вет5/0-5	0000

	.ФИНИШ 4630



	.АДРЕС 4100
Минимакс:
;Самый короткий и в то же время самый содержательный в шахматной программе. 
;Пусть позиция P3 некоторым образом уже оценена, и ее оценка равна Оц3. Тогда могут иметь место три возможности.
;1)Оц1>=Оц3. Это означает, что позиция P2 неприемлема для белых. 
;	Ход черных, ведущий в позицию P3, заносится в ЛХ2 (лучший ход), 
;	и остальные ходы черных из P2 уже рассматриваться не будут. Оценка позиции P2 при этом будет равна Оц3.
;2)Оц1<Оц3. Тогда сразу "закрыть" белых черные не могут. Возникает два случая.
;а) Оц3>Оц2. Это означает, что последний ход черных лучше всех, рассмотренных до него. Этот ход заносится в ЛХ2, а Оц3 переносится в Оц2.
;б)Оц3=<Оц2. Это означает, что ход черных был не лучшим, и они продолжат перебор.
;Всем этим и занимается блок "Минимакс": в случае 1) он вырабатывает на выходе сигнал ω=0, а в случае 2)ω=1.
;Таким образом, при ω=1 "Общая схема" продолжит перебор ходов на данном уровне, а при ω=0 вернется к предыдущей позиции и сменит в ней ход.

;перед обращением к Минимаксу в РА находится уровень перебора

		вм	{4000.0.4000}	(ОЦ1)	R0		;оценки содержатся в I и III адресах, возможно старший бит что-то еще значит?
		вм	(ОЦ0-1)	(ОЦ1)	0			;Оц1>=Оц3?
		пу	0	Минимакс_6	0               ;да - перейти (случай 1)
		вм	(ОЦ0)	R0	0			;Оц3=<Оц2?
		пу	{075.0.0.0}	Ω-1	Ω-1             ;да - cформировать ω=1 и выйти, случай 2Б
Минимакс_6:	пб	(a0)	веткаΔ	(Лх0)			;случаи 1 или 2А: занести ход 1->2 в лучший (?и сформировать информацию для Печати ветки?)
Минимакс_7:	пб	R0	Ω	(ОЦ0)			;Оц2:=Оц3 и выход


  	.НЕПЕЧАТЬ
	.ВСТАВИТЬ "../b61_const.inc"
	.ВСТАВИТЬ "../chess_const.inc"
