;Программа Печать хода.
;Стр. 7, адрес 5112.
;Экспорт произведен вручную из Excel 10.01.2022
;проведены изменения в названиях меток, ячеек и программ
;при ассемблировании выдает машинный код, аналогичный исходному
;Арладисс:
;"Выпечатывает 3 строки.
;В 1-ой - ход, сделанный машиной (76 55)
;Во 2-й - оценка (204 1735)
;В 3-й - количество рассмотренных позиций (148.357).
;Ход 7655 означает, что фигура пошла с 76-го поля на 55-е."

;ξ1 во II адресе содержит ход, который мы печатаем
;ОЦ-0 содержит какую-то оценку в 9 старших разрядах Iа (меньшую) и какую-то в IIIа (большую)
;λ содержит число рассмотренных позиций - вот почему есть программы Стоп по λ и Перемотка по λ
;	формат λ - целое двоичное число (начинается от младших разрядов, порядок 0)

;Возврат через δ1 - вероятно, чтобы вызываемые отсюда программы могли спокойно использовать Ω.
;И тогда программа Пq1 берет информацию из δ0 - на единицу меньше ячейки возврата.

;Программа печати хода управляется РА: по значению РА определяется, какую оценку из массива нужно брать.
;Программа печати хода управляется αвыд: если αвыд.II = 0, печатается 3 строки, см.выше, иначе - только ход
;αвыд заполняется во Внешней схеме и используется также в Печати ветки (ветка 2).
 

	.АДРЕС 5112
	.СТАРТ 5112
        или	0	Ω	δ1			;для возврата из Пq-1 по окончанию печати
        сдма	0072	ξ1	αпеч			;сдвиг на 6 разрядов вправо
        пв	*+1	А	Ω			;в γпеч теперь подготовлены 2 младших тетрады
        или	γпеч	{7760.0.0377}	R6              ;111 111 110 000-000 000 000 000-000 011 111 111
        						;444 433 332 222 111 1pp pp4 444 333 322 221 111
        						;1111 (0fh) печатается как пропуск тоже, так что
        						;остается два двухзначных дес. числа и пропуск между ними.
        пв	П1	*+1	биб2>10			;подменяем заголовок 2-10 уходом на П1
        и	(ОЦ0)	{7770.0.0}	R0
        сдса	0061	R0	R0			;сдвиг на 15дес разрядов вправо
							;теперь 3 старших 8-ричн числа из Iа ОЦ-1 находятся в 3 младших IIа R0
        и	(ОЦ0)	{0.0.F}	R1
        или	R0	R1	αпеч			;теперь в αпеч IIIа ОЦ0 на том же месте и Ia ОЦ0 во IIа, сдвинутый на 3 к младшим
        пв	*+1	А	Ω
        см	γпеч	7d	R7			;7=1 03 7000 0000 0000 - добавили пропуск вместо нуля к меньшей (трехзначной) оценке
        см	{144.0.0.0}	λ	R0		;ставим порядок 44 (дес.36), равный числу разрядов мантиссы,
							;то есть делаем так, чтобы число в ячейке рассматривалось как целое двоичное
        с	0	R0	αпеч                    ;нормализуем
        пв	*+1	эрз2>10	Ω			;настоящее преобразование 2->10
        или	0	γпеч	R10
        и	αвыд	{0.F.0}	0			;установка сигнала ω
        пу	q0	*+2	δ0			;если в αвыд.II был 0,пропустить следующую, δ0=q0 (старт.адр.печ=кон.адр.печ.)
        см	q0	{0.0.2}	δ0			;иначе δ0=q0+2, печать 3 адресов
        пв	δ1	Пq1	Ω
q0:     000	R6	4000	R6
 ;-------заглушка для 2->10
П1:     ра	(0000)	(0000)	Ω-1			;в предконец записать восстановление РА текущим значением
        пб	αпеч	Ω	γпеч			;выйти, передав вход на выход



  	.НЕПЕЧАТЬ
	.ВСТАВИТЬ "../b61_const.inc"
	.ВСТАВИТЬ "../chess_const.inc"

	.ФИНИШ 5112